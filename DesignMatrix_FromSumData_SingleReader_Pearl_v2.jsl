// ----------------------------------------------------------------------------
//
// Script to create Design Matrix for Single TW Pearl wafers either with CHM or Trilayer process
// to run Quasi data through Ultra Runner
//  
// JMP 12
//  
// Version 1.0
// Author: Manjit P.
// Date  : 09/06/2018
// Revision History
// No.	When		 	Who				What
// 2.0	9/6/18	Manjit P.	Corrected Sh calc formula for Rmin-Rlead for CHM vs Trilayer
// 							Updated filters for chip data removing <0 & >200
//					
//-----------------------------------------------------------------------------


Clear Log();
clear Globals();
Include("C:\Design\Scripts\JMP Scripts\GO-DataAnalysis\Manjit\JMP\login.jsl");

// Provide wafer numbers separated by comma

wafer_list = "296756,296769,296770,296774";



SP = Dialog(Vlist("CHM or Trilayer?", SelectProcess = RadioButtons("CHM", "Trilayer")));
::Select_Process=SP["SelectProcess"];

impala3 = "DSN=BDP3-Impala-Prod;";    // BDP database connection
DB2 = "DSN="||DSN_helga||";UID="||UID_helga||";PWD="||PWD_helga||";";  // for DB2
DB2archived = "DSN="||DSN_arc||";UID="||UID_arc||";PWD="||PWD_arc||";";  // for DB2

// TW grouping in Pearl format

twgroup = New Table( "tw_group_Quad",
	Add Rows( 22 ),
	New Column( "tw_group", Character, "Nominal",
		Set Values( {"TW1","TW1","TW1","TW1","TW1","TW1","TW2","TW2","TW2","TW2","TW2","TW2","TW2","TW2","TW3","TW3","TW3","TW3","TW3","TW3","TW3","TW3"} )
	),
	New Column( "quad", Numeric, "Continuous",
		Set Values( [1,4,7,10,15,20,3,6,8,11,14,18,19,22,2,5,9,12,13,16,17,21] )
	)
);


// Chimp summary data
// Original table: WAFTOOLDET.CHIMP
// Archived table: SNJFABAR.TMP9394_CHIMPARC_0


chimp_db2_dt = open database(DB2,
     "SELECT * 
      FROM  WAFTOOLDET.CHIMP
      WHERE WAFERNUM IN ("||wafer_list||") and 
      op = '3557' and
      PARMNAME in ('RMIN1', 'LRMIN')
      " 
      , "CHIMP_DET"    
    );

dt1 = chimp_db2_dt << stack (
					columns(
						:COL01, :COL02, :COL03, :COL04, :COL05, :COL06, :COL07, :COL08, :COL09, :COL10, 
						:COL11, :COL12, :COL13, :COL14, :COL15, :COL16, :COL17, :COL18, :COL19, :COL20, 
						:COL21, :COL22, :COL23, :COL24, :COL25, :COL26, :COL27, :COL28, :COL29, :COL30, 
						:COL31, :COL32, :COL33, :COL34, :COL35, :COL36, :COL37, :COL38, :COL39, :COL40, 
						:COL41, :COL42, :COL43, :COL44, :COL45, :COL46, :COL47, :COL48, :COL49, :COL50, 
						:COL51, :COL52, :COL53, :COL54, :COL55, :COL56, :COL57, :COL58, :COL59, :COL60,
						:COL61, :COL62, :COL63, :COL64)
);

dt1 << Select where (:Data < -0 | :Data > 200 );
dt1 << delete rows;

dt1 << delete columns ("COLNUM");
dt1 << New column ("COLNUM", formula(right(:Label, 2)), eval formula);
dt1:COLNUM << delete formula;
dt1:COLNUM << Data Type (numeric);
dt1:COLNUM << Modeling type (continuous);
dt1 << delete columns ("Label");

dt2 = dt1 << Split (
				Split By (:PARMNAME),
				Split (:Data),
				Group (
					:WAFERNUM,
					:ROWNUM,
					:COLNUM,
					
				)
	
);


dt2 << New column("Reader", formula("Single"), evalformula);
dt2 << new column("quad", numeric, continuous, formula(Ceiling(:ROWNUM / 44)));
									
:Reader << Delete Formula;

dt2 << Update (	With(twgroup), Match Columns( :quad = quad));

close(chimp_db2_dt, no save);
close(dt1, no save);
close(twgroup, no save);


// Get RA and TMR from summary table

dtsum = open database(DB2,
     "SELECT WAFERNUM, PARMNAME, MEDIAN 
      FROM WAFTOOLSUM.CHIMP 
      WHERE WAFERNUM IN ("||wafer_list||") and 
      LASTVIEW = 'Y' and
      PARMNAME in ('KTCRA','TMRA1' )
      " 
      , "CHIMP_SUM"    
    );

dt3 = dtsum << Transpose(	columns( :MEDIAN ),	By( :WAFERNUM ), Label( :PARMNAME ), Output Table( "CHIMP_SUMT" ));
dt3 << Delete Columns("Label");

close(dtsum, no save);

// Update tables
dt2 << Update (	With(dt3), Match Columns( :WAFERNUM = :WAFERNUM));
close(dt3, no save);

// Change column names to mathc ultrarunner columns

dt2:WAFERNUM << set name("waf");
dt2:Reader << set name("reader");
dt2:KTCRA << set name("ra");
dt2:TMRA1 << set name("tmr");
dt2:LRMIN << set name( "Rlead");
dt2:RMIN1 << set name( "rmin");

// Add A4 SH information: 3000nm for Trilayer process and 600nm for CHM	

Match (Select_Process,	1, 
					(dt2 << new column("K3F", numeric, continuous, formula(600))),  
						2,
					(dt2 << new column("K3F", numeric, continuous, formula(3000))),
);



// Sumamrize to get Design Matrix

dt_sum=dt2<< Summary(
					Group( :waf, :reader, :tw_group ),
					Mean( :tmr ),
					Mean( :K3F ),
					Mean( :ra ),
					mean( :Rlead),
					Mean( :rmin ), link to original data table(0) );


Match (Select_Process,	1, 
					(dt_sum << New Column("Mean(rmin_rlead)", formula(:Name("Mean(rmin)") - :Name("Mean(Rlead)") + 2), evalformula)),  
						2,
					(dt_sum << New Column("Mean(rmin_rlead)", formula(:Name("Mean(rmin)") - :Name("Mean(Rlead)")), evalformula)),
);



dt_sum<< New Column("EXP",character);
dt_sum<< Move Selected Columns({"EXP"}, To First);
dt_sum << Delete Columns("N Rows");
dt_sum << New Column("tw", formula(Match(:tw_group, "TW1", "wide", "TW2", "mid", "TW3", "narr")), eval formula);
dt_sum << set name("_DesignMatrix");

close(dt2, no save);


